/*
 * Copyright 2013-2014 @BijdorpStudio
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    maven { url 'http://download.crashlytics.com/maven' }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.0.0'
    classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.7'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.6'
  }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'robolectric'
apply plugin: 'com.github.ben-manes.versions'

repositories {
  maven { url 'http://download.crashlytics.com/maven' }
  mavenCentral()
  jcenter()
}

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 21

    versionCode 10
    versionName "1.1"
  }

// Load signing keys only for release
  signingConfigs {
    release {
      if ( rootProject.file( 'keys/sign.properties' ).exists() )
      {
        Properties p = new Properties()
        p.load( new FileInputStream( rootProject.file( 'keys/sign.properties' ) ) )

        storeFile file( p.file )
        storePassword p.password
        keyAlias p.alias
        keyPassword p.keyPassword
      }
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }

  sourceSets {
    androidTest.setRoot( 'src/test' ) // For Robolectric
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  // Android
  compile 'com.android.support:support-v4:21.0.3'
  compile 'com.android.support:appcompat-v7:21.0.3'
  // Square
  compile 'com.squareup.okhttp:okhttp:1.6.0'
  compile 'com.squareup:otto:1.3.5'
  provided 'com.squareup.dagger:dagger-compiler:1.2.2'
  compile 'com.squareup.dagger:dagger:1.2.2'
  // Jake Wharton
  compile 'com.jakewharton:disklrucache:2.0.2'
  // Crashlytics
  compile 'com.crashlytics.android:crashlytics:1.1.13'
  // MixPanel
  compile 'com.mixpanel.android:mixpanel-android:4.5.1@aar'
  // NineOldAndroids
  compile 'com.nineoldandroids:library:2.4.0'

  // AS tests
  androidTestCompile 'junit:junit:4.12'
  androidTestCompile 'org.robolectric:robolectric:2.4'
  androidTestCompile 'com.squareup:fest-android:1.0.8'
  androidTestCompile 'org.mockito:mockito-all:1.10.17'
  androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'

  // tests
  testCompile 'junit:junit:4.12'
  testCompile 'org.robolectric:robolectric:2.4'
  testCompile 'com.squareup:fest-android:1.0.8'
  testCompile 'org.mockito:mockito-all:1.10.17'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile 'com.squareup.dagger:dagger-compiler:1.2.2'
}

task wrapper( type: Wrapper ) {
  gradleVersion = '2.2'
}